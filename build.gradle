import java.time.format.DateTimeFormatter

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.4'
	id 'org.ajoberstar.grgit' version '5.0.0'
}

apply plugin: 'io.spring.dependency-management'

def git = org.ajoberstar.grgit.Grgit.open(["currentDir": "${project.rootDir}"])

group = 'com.izeye.helloworld'
version = DateTimeFormatter.ofPattern("yyyyMMdd").format(git.head().dateTime) + "-" + git.head().abbreviatedId
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named("bootBuildImage") {
	imageName.set("izeye/hello-spring-boot:${project.version}")
	publish = true
	docker {
		publishRegistry {
			username = System.getenv("DOCKER_USERNAME")
			password = System.getenv("DOCKER_PASSWORD")
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named('wrapper') {
	gradleVersion = "8.0.2"
}
